allprojects {
    repositories {
        google()
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.13.1'
            force 'androidx.core:core-ktx:1.13.1'
        }
    }
    
    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
}

def newBuildDir = rootProject.layout.buildDirectory
    .dir("../../build")
    .get()
rootProject.layout.buildDirectory.value(newBuildDir)

subprojects {
    def newSubprojectBuildDir = newBuildDir.dir(project.name)
    project.layout.buildDirectory.value(newSubprojectBuildDir)
    
    if (name != 'app') {
        evaluationDependsOn(':app')
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

subprojects {
    def configureNamespace = { p ->
        try {
            if (p.hasProperty('android')) {
                if (p.android.namespace == null) {
                    def manifestFile = p.file('src/main/AndroidManifest.xml')
                    if (manifestFile.exists()) {
                        def contents = manifestFile.text
                        def matcher = contents =~ /package="([^"]+)"/
                        if (matcher.find()) {
                            p.android.namespace = matcher.group(1)
                        }
                    }
                }
            }
        } catch (Exception e) {
            // Ignore errors
        }
    }

    if (project.state.executed) {
        configureNamespace(project)
    } else {
        project.afterEvaluate { p ->
            configureNamespace(p)
        }
    }
}
