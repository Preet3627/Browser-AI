allprojects {
    repositories {
        google()
        mavenCentral()
    }
    
    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
}


def newBuildDir = rootProject.layout.buildDirectory
    .dir("../../build")
    .get()
rootProject.layout.buildDirectory.value(newBuildDir)

subprojects {
    def newSubprojectBuildDir = newBuildDir.dir(project.name)
    project.layout.buildDirectory.value(newSubprojectBuildDir)
}

subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            if (project.android.namespace == null) {
                def manifestFile = project.file('src/main/AndroidManifest.xml')
                if (manifestFile.exists()) {
                    def manifestXml = new XmlParser().parse(manifestFile)
                    def packageName = manifestXml.attribute('package')
                    if (packageName != null) {
                        project.android.namespace = packageName
                    }
                }
            }
        }
    }
}
